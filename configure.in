# configure.in - BinTec Remote CAPI Library
# written by Heiner Steven <heiner.steven@odn.de>
# @(#) configure.in	1.1 02/06/25
#
# Process this file with autoconf to produce a configure script.
AC_INIT(BinTec Remote CAPI, 1.7, support@bintec.de)
AC_CONFIG_SRCDIR([rcapi.c])
AC_CONFIG_HEADER([capiconf.h])

# Checks for programs.
AC_PROG_CC
AC_PROG_INSTALL
AC_PROG_RANLIB
AC_PROG_LN_S
AC_PROG_MAKE_SET

# Checks for libraries.
# If we can't find connect, try looking in -lsocket, -lnsl, and -linet.
# The Irix 5 libc.so has connect and gethostbyname, but Irix 5 also has
# libsocket.so which has a bad implementation of gethostbyname (it
# only looks in /etc/hosts), so we only look for -lsocket if we need
# it.
LIBS=""
AC_CHECK_FUNC(connect, :,
[case "$LIBS" in
*-lnsl*) ;;
*) AC_CHECK_LIB(nsl_s, printf) ;;
esac
case "$LIBS" in
*-lnsl*) ;;
*) AC_CHECK_LIB(nsl, printf) ;;
esac
case "$LIBS" in
*-lsocket*) ;;
*) AC_CHECK_LIB(socket, connect) ;;
esac
case "$LIBS" in
*-linet*) ;;
*) AC_CHECK_LIB(inet, connect) ;;
esac
])



# Checks for header files.
AC_HEADER_STDC
AC_CHECK_HEADERS([sys/types.h sys/socket.h arpa/inet.h errno.h fcntl.h memory.h netdb.h netinet/in.h stdlib.h string.h stdio.h sys/uio.h sys/poll.h sys/time.h unistd.h signal.h ctype.h])

# Checks for typedefs, structures, and compiler characteristics.
AC_TYPE_SIZE_T
AC_HEADER_TIME
AC_C_CONST
AC_C_PROTOTYPES

AC_MSG_CHECKING(for working void)
AC_TRY_COMPILE(, [
(void)(0);
], [
AC_MSG_RESULT(yes)
AC_DEFINE(HAVE_VOID, 1)
], [
AC_MSG_RESULT(no)
AC_DEFINE(HAVE_VOID, )
])

AC_CHECK_SIZEOF(unsigned short)
if test $ac_cv_sizeof_unsigned_short = 4
then
    AC_DEFINE(UINT4, unsigned short)
else
    AC_CHECK_SIZEOF(unsigned int)
    if test $ac_cv_sizeof_unsigned_int = 4
    then
	AC_DEFINE(UINT4, unsigned int)
    else
	AC_CHECK_SIZEOF(unsigned long)
	if test $ac_cv_sizeof_unsigned_long = 4
	then
	    AC_DEFINE(UINT4, unsigned long)
	else
	    AC_CHECK_SIZEOF(unsigned long long)
	    if test $ac_cv_sizeof_unsigned_long_long = 4
	    then
		AC_DEFINE(UINT4, unsigned long long)
	    else
		AC_MSG_ERROR([cannot determine data type of size 4])
	    fi
	fi
    fi
fi

# Checks for library functions.
AC_FUNC_FORK
AC_FUNC_MALLOC
AC_FUNC_STAT
AC_TYPE_SIGNAL
AC_FUNC_SETPGRP

# The AC_CHECK_FUNCS macto creates a variable ac_cv_func_FUNCNAME with a
# content of "yes" or "no". We are prepared to use our own version of
# the library functions from ac_opt_functions, and we need the functions
# in ac_req_functions

ac_opt_functions="poll"
for ac_func in $ac_opt_functions
do
    eval "ac_port_func_$ac_func=yes"
done

ac_req_functions="$ac_opt_functions gethostbyname memset select socket strtol"

AC_CHECK_FUNCS($ac_req_functions)

AC_SUBST(PORTOBJ)
PORTOBJ=
MISSINGFUNC=
for ac_func in $ac_req_functions
do
    if eval test "\$ac_cv_func_$ac_func = no"
    then
        if eval test X"\$ac_port_func_$ac_func" = X
        then MISSINGFUNC="$MISSINGFUNC $ac_func"
        else PORTOBJ="$PORTOBJ $ac_func.o"
        fi
    fi
done

#echo >&2 "PORTOBJ=<$PORTOBJ>"
#echo >&2 "MISSINGFUNC=<$MISSINGFUNC>"

test -n "$MISSINGFUNC" && {
AC_MSG_ERROR([missing function(s):$MISSINGFUNC])
}

AC_CONFIG_FILES([Makefile])
AC_OUTPUT
